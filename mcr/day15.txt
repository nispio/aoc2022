startmacro/quiet A:infile
local tempfiles line input idx sx sy bx by
res tempfiles "indata sensors spans"
erase/nowarn ^tempfiles

csvconvert/delim="=" infile indata 3000 f1/2A f2/2A f3/5A f4/2A f5/L
f3000/size=indata(hcb.sz) sensors pos/CL bcn/CL rng/L
loop indata(hcb.sz) n
  sedit indata(n;2) L:sx ELEMENT 1 ","
  sedit indata(n;3) L:sy ELEMENT 1 ":"
  sedit indata(n;4) L:bx ELEMENT 1 ","
  res L:by indata(n;5)
  calc sensors(n;~pos) (^sx,^sy)
  calc sensors(n;~bcn) (^bx,^by)
  calc sensors(n;~rng) sx bx - ABS sy by - ABS +
endloop

print " === First Star === "
res L:y 2e6
f3000/size=sensors(hcb.sz) spans xmin/L xmax/L stat/L
loop sensors(hcb.sz) n
  res L:v sensors(n;~pos)
  res L:r sensors(n;~rng)
  calc L:m r v(2) y - ABS -
  if m GE 0
    calc spans(n;~xmin) v(1) m -
    calc spans(n;~xmax) v(1) m +
    calc spans(n;~stat) 0
  endif
endloop

res L:total 0
res L:index 0
while 1
  res curr 0
  loop spans(hcb.sz) n
    if spans(n;~stat) EQ 0 res curr n
  endloop
  if curr EQ 0 break
  calc xmin spans(curr;~xmin)
  calc xmax spans(curr;~xmax)
  calc spans(curr;~stat) 1
  res merging 1
  while merging GT 0
  res merging 0
  loop spans(hcb.sz) n
    if spans(n;~stat) EQ 0 AND xmin LE spans(n;~xmax) AND xmax GE spans(n;~xmin)
      calc merging merging 1 +
      calc xmin xmin spans(n;~xmin) MIN
      calc xmax xmax spans(n;~xmax) MAX
      calc spans(n;~stat) 1
    endif
  endloop
  endwhile
  calc total total xmax xmin - +
endwhile
print "^total"

print " === Second Star === "
f3000/size=sensors(hcb.sz) spans xmin/L xmax/L stat/L
envi file open sensors
envi file open spans
loop 4e6 y
  loop sensors(hcb.sz) n
    res L:v sensors(n;~pos)
    res L:r sensors(n;~rng)
    calc L:m r v(2) y - ABS -
    calc spans(n;~stat) 1
    if m GE 0
      calc spans(n;~xmin) v(1) m -
      calc spans(n;~xmax) v(1) m +
      calc spans(n;~stat) 0
    endif
  endloop

  calc xmin spans(1;~xmin)
  calc xmax spans(1;~xmax)
  calc spans(1;~stat) 1
  res merging 1
  while merging GT 0
    res merging 0
    loop spans(hcb.sz) n
      if spans(n;~stat) EQ 0
        if xmin LE spans(n;~xmax) AND xmax GE spans(n;~xmin)
          calc merging merging 1 +
          calc xmin xmin spans(n;~xmin) MIN
          calc xmax xmax spans(n;~xmax) MAX
          calc spans(n;~stat) 1
        endif
      endif
    endloop
  endwhile
  if xmin GT 0 OR xmax LT 4e6 break
endloop

calc x xmin 1 - xmax 1 + xmin 0 GT X?Z:Y
calc z x 4e6 * y +
print "^z (^{x},^{y})"

envi file close *

erase ^tempfiles
endmacro
